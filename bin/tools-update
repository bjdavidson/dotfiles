#!/usr/bin/env zsh
# vi: ft=zsh :

set -eu

autoload colors
colors

local orig_dir=$(pwd -L)

local vim_chksum="$(md5sum ~/.vimrc)"

echo "$fg[blue]" 'ðŸ”¨ Updating dotfiles and toolsðŸ”§ ' "$reset_color"

if [ ! -d ~/.zsh/plugins/zsh-syntax-highlighting ]; then
  mkdir -p ~/.zsh/plugins
  git clone git://github.com/zsh-users/zsh-syntax-highlighting.git ~/.zsh/plugins/zsh-syntax-highlighting
fi

local dir
for dir in \
  ~/.dotfiles \
  ~/.dotfiles-personal \
  ~/.oh-my-zsh \
  ~/.zsh/plugins/zsh-syntax-highlighting
do
  cd "${dir}"
  echo
  echo "${fg[blue]}===> ${fg[green]}$( pwd -L )${reset_color}"
  if git diff-index --quiet HEAD; then
    git pull --rebase
  else
    echo "${fg[red]} * skipping -- checkout is dirty${reset_color}"
    git status -s -b
  fi
  git log '@{u}..'
done

echo
if (( $+commands[rcup] )); then
  echo "${fg[blue]}===> ${fg[green]}rcup${reset_color}"
  rcup
else
  echo "You need to install 'rcup'." 1>&2
fi

cd "${orig_dir}"

local new_vim_chksum="$(md5sum ~/.vimrc)"
if [ "${vim_chksum}" != "${new_vim_chksum}" ]; then
  # Install any new vim plugins.
  vim -c ':PluginInstall' -c ':qa'
fi

echo
echo -n "$fg[green]"
echo -n "You may wish to consider running 'rbenv-update-plugins' or 'update-gems'"
echo "${reset_color}"

# EOF
